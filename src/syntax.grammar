@top Program { expression* }

@skip { space }

expression {
  Number |
  Identifier |
  String |
  Boolean |
  Null |
  Operator |
  OperatorKeyword |
  Punctuation |
  NullSafe |
  NullCoalescing |
  Object { "{" (ObjectKey ":" expression ("," ObjectKey ":" expression)?) "}" } |
  Array { "[" (expression ("," expression)*)? "]" } |
  Application { OpeningBracket expression* ClosingBracket }
}

@tokens {
  Number { (('.' $[0-9]+('_' $[0-9]+)* | $[0-9]+('_' $[0-9]+)*('.' (![.])($[0-9]+('_' $[0-9]+)*)?)?)($[eE]$[+-]?$[0-9]+('_' $[0-9]+)*)?) }
  Identifier { $[a-zA-Z_]+$[a-zA-Z_0-9]* }
  String { ( '"' (!["\\] | "\\" _)* ('"' | @eof) ) | "'" (!['\\] | "\\" _)* ("'" | @eof) }
  ObjectKey { $[a-zA-Z_]+$[a-zA-Z_0-9]* | String }
  Null { "null" }
  Boolean { "true" | "false" }

  space { $[ \t\n\r]+ }

  OperatorKeyword { 'starts with' | 'ends with' | 'contains' | 'matches' | 'not in' | 'not' | 'and' | 'or' | 'in' }
  Operator { '===' | '!==' |  '||' | '&&' | '==' | '!=' | '>=' | '<=' | '..' | '**' | '!' | '|' | '^' | '&' | '<' | '>' | '+' | '-' | '~' | '*' | '/' | '%' }

  OpeningBracket { "(" }
  ClosingBracket { ")" }
  NullSafe { '?.' }
  NullCoalescing { '??' }
  Punctuation { $[.?:,] }

  @precedence { Number, Punctuation }
  @precedence { OperatorKeyword, Identifier }
  @precedence { Boolean, Identifier }
  @precedence { Null, Identifier }
  @precedence { Number, Identifier }
}

@detectDelim
