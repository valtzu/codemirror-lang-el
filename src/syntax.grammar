
@detectDelim

@precedence {
  left @left,
  right @right
}

@top Expression { expression }

@skip { space }

expression {
  ObjectAccess |
  ArrayAccess |
  FunctionCall |
  Number |
  String |
  Boolean |
  Null |
  Object |
  Array |
  Identifier |
  TernaryExpression |
  BinaryExpression |
  UnaryExpression |
  "(" expression ")"
}

Object {
  "{" (expression ":" expression ("," expression ":" expression)*)? "}"
}

ObjectAccess {
  expression !left (NullSafeMemberOf | MemberOf) Identifier
}

ArrayAccess {
  expression !left "[" expression "]"
}

FunctionCall {
  (Identifier | ObjectAccess) "(" (expression ("," expression)*)? ")"
}

Array {
  "[" (expression ("," expression)*)? "]"
}

UnaryExpression {
  UnaryOperator !right expression
}

BinaryExpression {
  expression !right (OperatorKeyword | Operator) expression
}

TernaryExpression {
  expression !right '?' expression ':' expression
}

@tokens {
  Number { (($[0-9]+('_' $[0-9]+)*('.' (![.])($[0-9]+('_' $[0-9]+)*)?)?)($[eE]$[+-]?$[0-9]+('_' $[0-9]+)*)?) }
  Identifier { $[a-zA-Z_]+$[a-zA-Z_0-9]* }
  String { ( '"' (!["\\] | "\\" _)* ('"' | @eof) ) | "'" (!['\\] | "\\" _)* ("'" | @eof) }

  space { $[ \t\n\r]+ }

  Operator { '===' | '!==' |  '||' | '&&' | '==' | '!=' | '>=' | '<=' | '..' | '**' | '!' | '|' | '^' | '&' | '<' | '>' | '+' | '-' | '~' | '*' | '/' | '%' | '??' | '?:' }
  MemberOf { '.' }
  NullSafeMemberOf { '?.' }

  @precedence {
    String,
    Operator,
    NullSafeMemberOf,
    MemberOf,
    Number,
    Identifier
  }
}

o<term> { @extend<Operator, term> }
k<term> { @specialize<Identifier, term> }

Boolean { k<'true'> | k<'false'> }
Null { k<'null'> }

OperatorKeyword {
    (k<'starts'> k<'with'>) |
    (k<'ends'> k<'with'>) |
    k<'contains'> |
    k<'matches'> |
    (k<'not'> k<'in'>) |
    k<'not'> |
    k<'and'> |
    k<'or'> |
    k<'in'>
}

UnaryOperator {
    k<'not'> |
    o<'!'> |
    o<'+'> |
    o<'-'>
}
