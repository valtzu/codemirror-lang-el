@top Program { expression* }

@skip { space }

expression {
  Number |
  Identifier |
  String |
  Boolean |
  Operator |
  Punctuation |
  NullSafe |
  NullCoalescing |
  Array { "[" (expression ("," expression)*)? "]" } |
  Application { OpeningBracket expression* ClosingBracket }
}

@tokens {
  Number { (('.' $[0-9]+('_' $[0-9]+)* | $[0-9]+('_' $[0-9]+)*('.' (![.])($[0-9]+('_' $[0-9]+)*)?)?)($[eE]$[+-]?$[0-9]+('_' $[0-9]+)*)?) }

  Identifier { $[a-zA-Z_]+$[a-zA-Z_0-9]* }

  String { ( '"' (!["\\] | "\\" _)* '"' ) | "'" (!['\\] | "\\" _)* "'" }

  Boolean { "true" | "false" }

  space { $[ \t\n\r]+ }

  Operator { 'starts with' | 'ends with' | 'contains' | 'matches' | 'not in' | 'not' | 'and' | '===' | '!==' | 'or' | '||' | '&&' | '==' | '!=' | '>=' | '<=' | 'in' | '..' | '**' | '!' | '|' | '^' | '&' | '<' | '>' | '+' | '-' | '~' | '*' | '/' | '%' }

  OpeningBracket { $[\(\{] }
  ClosingBracket { $[\)\}] }
  NullSafe { '?.' }
  NullCoalescing { '??' }
  Punctuation { $[.?:] }

  @precedence { Number, Punctuation }
  @precedence { Operator, Identifier }
  @precedence { Boolean, Identifier }
  @precedence { Number, Identifier }
}

@detectDelim
