
@detectDelim

@precedence {
  left @left,
  right @right
}

@skip { space }

@top Expression { expression }

expression {
  ObjectAccess<Property> |
  ArrayAccess |
  Call |
  Number |
  String |
  Boolean |
  Null |
  Object |
  Array |
  Variable |
  TernaryExpression |
  BinaryExpression |
  UnaryExpression |
  Application { "(" expression ")" }
}

Variable { word }
Function { word }
Property { word }
Method { word }

list {
  (expression ("," expression)*)?
}

Object {
  "{" (expression ":" expression ("," expression ":" expression)*)? "}"
}

ArrayAccess {
  expression !left "[" expression "]"
}

Array {
  "[" list "]"
}

UnaryExpression {
  UnaryOperator !right expression
}

BinaryExpression {
  expression !right (OperatorKeyword | Operator) expression
}

ObjectAccess<member> {
  expression !left ( MemberOf | NullSafeMemberOf ) member
}

call<callable> {
  callable Arguments { "(" list ")" }
}

Call {
  call<Function> |
  call<ObjectAccess<Method>>
}

TernaryExpression {
  expression !right o<'?'> expression ':' expression
}

@tokens {
  Number { (($[0-9]+('_' $[0-9]+)*('.' (![.])($[0-9]+('_' $[0-9]+)*)?)?)($[eE]$[+-]?$[0-9]+('_' $[0-9]+)*)?) }
  word { $[a-zA-Z_]+$[a-zA-Z_0-9]* }
  String { ( '"' (!["\\] | "\\" _)* ('"' | @eof) ) | "'" (!['\\] | "\\" _)* ("'" | @eof) }

  space { $[ \t\n\r]+ }

  Operator { '===' | '!==' |  '||' | '&&' | '==' | '!=' | '>=' | '<=' | '..' | '**' | '!' | '|' | '^' | '&' | '<' | '>' | '+' | '-' | '~' | '*' | '/' | '%' | '??' | '?:' | '?' }
  MemberOf { '.' }
  NullSafeMemberOf { '?.' }

  @precedence {
    String,
    NullSafeMemberOf,
    Operator,
    MemberOf,
    Number,
    word
  }
}

o<term> { @extend<Operator, term> }
k<term> { @specialize<word, term> }

Boolean { k<'true'> | k<'false'> }
Null { k<'null'> }

OperatorKeyword {
    (k<'starts'> k<'with'>) |
    (k<'ends'> k<'with'>) |
    k<'contains'> |
    k<'matches'> |
    (k<'not'> k<'in'>) |
    k<'not'> |
    k<'and'> |
    k<'or'> |
    k<'in'>
}

UnaryOperator {
    k<'not'> |
    o<'!'> |
    o<'+'> |
    o<'-'>
}
